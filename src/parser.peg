%value "struct ast"
%auxil "FILE*"

%source {
#include <stdio.h>
#include <string.h>
#include <stdlib.h>
}

%header {
#include "common.h"
#define PCC_GETCHAR(AUX) fgetc(AUX)
}

top <- _* l:list { $$ = l; } / _* !.

list <- '(' _*            ')' { $$ = alist0(); }
      / '(' _* a:atom  _* ')' { $$ = alist1(a); }
      / '(' _* i:inner _* ')' { $$ = i; }

inner <- d:dot  { $$ = d; }
	   / m:many { $$ = m; }

dot <- l:subdot _+ '.' _+ r:subdot { $$ = append(l, r); }

subdot <- i:inner { $$ = i; }
        / a:atom  { $$ = alist1(a); }

many <- m:many _+ a:atom { $$ = append(m, a); }
      / b:atom _+ c:atom { $$ = append(alist1(b), c); }

atom <- l:list    { $$ = l; }
      / k:keyword { $$ = k; }
      / o:oper    { $$ = o; }
      / i:integer { $$ = i; }
      / r:ref     { $$ = r; }
      / d:diamond { $$ = d; }

integer <- [0-9]+ { $$ = aint(strtol($0, 0, 10)); }

keyword <- ':' word { $$ = akw(strdup($0 + 1)); }

ref <- word { $$ = aref(strdup($0)); }

word <- [_a-zA-Z] [_a-zA-Z0-9-]*

oper <- !'.' opchar    { $$ = aoper(strdup($0), 0, 0); }
      / opchar opchar+ { $$ = aoper(strdup($0), 0, 0); }

opchar <- [!#$%&*+-./:;<=>?@\[\\\]^_`{|}~]

diamond <- '<' [_a-zA-Z0-9-.]+ '>' { $$ = adiamond(strdup($0)); }

_ <- [ \t\n]

# 0123456789
# ABCDEFGHIJKLMNOPQRSTUVWXYZ
# abcdefghijklmnopqrstuvwxyz
# !"#$%&'()*+,-./:;<=>?@[\]^_`{|}~

